# -*- Mode: Python -*-

smp_ap_code = "AP_CODE=0x1000"

freestanding_env = Environment()

freestanding_env['CFLAGS']  = "-Os -minline-all-stringops -m32 -march=pentium -mtune=core2 -pipe -std=gnu99 -ffreestanding -nostdlib -Wno-multichar -Wall"
freestanding_env['CFLAGS'] += " -ffunction-sections -fomit-frame-pointer -mregparm=3 "
freestanding_env['LINKFLAGS'] = "-m elf_i386 -gc-sections -N"
freestanding_env['LINK'] = "ld"
freestanding_env['ASFLAGS'] = "-O5 -f elf32 -D" + smp_ap_code

def CheckCommand(context, cmd):
       context.Message('Checking for %s command... ' % cmd)
       result = WhereIs(cmd)
       context.Result(result is not None)
       return result

conf = Configure(freestanding_env, custom_tests = {'CheckCommand' : CheckCommand})

if not conf.CheckCommand("yasm"):
    if not conf.CheckCommand("nasm"):
        print("Please install yasm or nasm!")
        Exit(1)
    else:
        conf.env['AS'] = "nasm"
else:
    conf.env['AS'] = "yasm"

if not (conf.CheckCHeader("stdint.h") and
        conf.CheckCHeader("stdarg.h")):
    print("Standard C headers are missing.")
    Exit(1)

freestanding_env = conf.Finish()

fenv = freestanding_env.Clone()

stand = fenv.StaticLibrary('stand',
                           [ 'elf.c',
                             'hexdump.c',
                             'mbi.c',
                             'mbi2.c',
                             'mbi-tools.c',
                             'pci.c',
                             'pci_db.c',
                             'printf.c',
                             'reboot.c',
                             'serial.c',
                             'start.asm',
                             'util.c',
                             'version.c',
                             'vga.c',

                             # libc stuff
                             'memcpy.c',
                             'memcmp.c',
                             'memset.c',
                             'strlen.c',

                             # FreeBSD libc stuff
                             'strncmp.c',
                             'strncpy.c',
                             'strtok.c',
                             'strtoull.c',
                             'qdivrem.c',
                             ])

tinf = fenv.StaticLibrary('tinf',
                          [ 'tinflate.c',
                            'tinfgzip.c',
                            'tinfzlib.c',
                            'adler32.c',
                            'crc32.c',
                            ])

fenv['CPPPATH'] = ["../include", "include/"]
fenv['LIBPATH'] = ['.']

fenv_microcode = fenv.Clone()

fenv['LINKFLAGS'] += " -T " + File('morbo.ld').srcnode().path
fenv_microcode['LINKFLAGS'] += " -T " + File('microcode.ld').srcnode().path + " -defsym=" + smp_ap_code

# Execute git describe somewhere where our code is. This is useful,
# when this SConscript is used by third-party scons build systems
# directly.
AlwaysBuild(Command('version.inc', ['include/version.h'], """( cd `dirname $SOURCE` && git describe --dirty || echo UNKNOWN ) | sed 's/^\\(.*\\)$/"\\1"/' > $TARGET"""))

def DoInstall(prog):
    Depends(prog, 'morbo.ld')
    Depends(prog, 'microcode.ld')
    Install('../tftp', prog)

# Morbo

DoInstall(fenv.Program('morbo',
                       [ 'crc16.c',
                         'morbo.c',
                         'ohci.c' ],
                       LIBS=['stand', 'tinf']))

# Zapp

DoInstall(fenv.Program('zapp',
                       [ 'acpi.c',
                         'zapp.c',
                         ],
                       LIBS=['stand', 'tinf']))

# Bender

DoInstall(fenv.Program('bender',
                       [ 'bender.c',
                         ],
                       LIBS=['stand', 'tinf']))

# Unzip

DoInstall(fenv.Program('unzip',
                       [ 'unzip.c',
                         ],
                       LIBS=['stand', 'tinf']))

# Farnsworth

DoInstall(fenv.Program('farnsworth',
                       [ 'farnsworth.c',
                         ],
                       LIBS=['stand', 'tinf']))

# Performance tests

DoInstall(fenv.Program('basicperf',
                       [ 'basicperf.c' ],
                       LIBS=['stand']))

# Microcode

DoInstall(fenv_microcode.Program('microcode',
                                 [ 'microcode.c',
                                   'acpi.c' ],
                                 LIBS=['stand', 'tinf']))
# EOF
